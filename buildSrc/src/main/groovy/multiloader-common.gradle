plugins {
    id 'java-library'
}

base {
    archivesName = "fastregions-${project.name}-${modVersion}-${minecraftVersion}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups('org.spongepowered') }
    }
    exclusiveContent {
        forRepositories(maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org/'
        },
                maven {
                    name = "NeoForge"
                    url = 'https://maven.neoforged.net/releases'
                })
        filter { includeGroup('org.parchmentmc.data') }
    }
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("dev.tonimatas.fastregions:${base.archivesName.get()}:$modVersion")
        capability("dev.tonimatas.fastregions:fastregions-${project.name}-${minecraftVersion}:$modVersion")
        capability("dev.tonimatas.fastregions:fastregions:$modVersion")
    }
}

sourcesJar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_FastRegions" }
    }
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_FastRegions" }
    }

    manifest {
        attributes(['Specification-Title'   : "FastRegions",
                    'Specification-Vendor'  : "TonimatasDEV",
                    'Specification-Version' : project.jar.archiveVersion,
                    'Implementation-Title'  : project.name,
                    'Implementation-Version': project.jar.archiveVersion,
                    'Implementation-Vendor' : "TonimatasDEV",
                    'Built-On-Minecraft'    : minecraftVersion])
    }
}

processResources {
    var expandProps = ['modVersion'            : modVersion,
                       'minecraftVersion'      : minecraftVersion,
                       'minecraftVersionRange' : minecraftVersionRange,
                       'fabricVersion'         : fabricVersion,
                       'fabricLoaderVersion'   : fabricLoaderVersion,
                       'neoforgeVersion'       : neoforgeVersion]

    filesMatching(['fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}
